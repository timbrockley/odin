// This file takes care of loading the bindgen config and then runs procs in the `translate_X.odin`
// files and the `output.odin` file in order to generate the bindings.
package bindgen2

import vmem "core:mem/virtual"
import "core:mem"
import "core:os"
import "core:fmt"
import "core:strings"
import "core:path/filepath"
import "base:runtime"
import "core:encoding/json"
import "core:slice"
import "core:log"

main :: proc() {
	permanent_arena: vmem.Arena
	permanent_allocator := vmem.arena_allocator(&permanent_arena)
	context.allocator = permanent_allocator
	context.temp_allocator = permanent_allocator
	context.logger = log.create_console_logger()

	ensure(len(os.args) == 2, "Usage: 'bindgen directory' or 'bindgen directory/bindgen.sjson'")
	config_dir_or_file := os.args[1]

	DEFAULT_CONFIG_FILENAME :: "bindgen.sjson"
	
	config_filename: string
	dir: string

	filepath_join :: proc(strings: []string) -> string {
		p, _ := filepath.join(strings, context.allocator)
		return p
	}

	// Check if the path the user points to is a directory or a .sjson file. The user can supply one
	// or the other. If the path the user points to is a file, then the directory where the file
	// lives will be used a "working directory."
	if strings.has_suffix(config_dir_or_file, ".sjson") && os.is_file(config_dir_or_file) {
		config_filename = config_dir_or_file
		dir = filepath.dir(config_dir_or_file)
	} else if os.is_dir(config_dir_or_file) {
		config_filename = filepath_join({config_dir_or_file, DEFAULT_CONFIG_FILENAME})
		dir = config_dir_or_file
	} else {
		fmt.panicf("%v is not a directory nor a valid config file", config_dir_or_file)
	}

	// These are almost never used. The are a fallback if it fails to stat the directory below.
	default_output_folder := "output"
	default_package_name := "pkg"

	if config_dir_handle, config_dir_handle_err := os.open(dir); config_dir_handle_err == nil {
		if stat, stat_err := os.fstat(config_dir_handle, context.allocator); stat_err == nil {
			default_output_folder = stat.name
			default_package_name = stat.name
		}
	}

	config: Config

	if os.is_file(config_filename) {
		if config_data, config_data_err := os.read_entire_file(config_filename, context.allocator); config_data_err == nil {
			config_err := json.unmarshal(config_data, &config, .SJSON)
			fmt.ensuref(
				config_err == nil,
				"Failed parsing config %v: %v",
				config_filename,
				config_err,
			)
		} else {
			fmt.ensuref(config_data_err == nil, "Failed parsing config %v", config_filename)
		}
	} else {
		// If the user points to a directory and there is no `bindgen.sjson` inside it, then we will
		// set the folder itself as input. This is a "quick and dirty" path to generating bindings:
		// You do not really need a config for very simple things.
		config.inputs = slice.clone([]string{"."})
	}

	// The working dir of the program may not be the same as `dir`. Therefore, we add `dir` to any
	// path that comes out of the config.
	//
	// Initially, I changed the working directory (using some OS proc) instead of adding `dir` to
	// the path, but it lead to other problems.
	output_folder := filepath_join({dir, config.output_folder != "" ? config.output_folder : default_output_folder})
	package_name := config.package_name != "" ? config.package_name : default_package_name
	
	if config.imports_file != "" {
		config.imports_file = filepath_join({dir, config.imports_file})
	}

	for enum_name, prefix in config.remove_enum_member_prefix {
		if new_enum, exists := config.bit_setify[enum_name]; exists {
			config.remove_enum_member_prefix[new_enum] = prefix
		}
	}

	input_files: [dynamic]string

	for input_base in config.inputs {
		input := filepath_join({dir, input_base})
		if os.is_dir(input) {
			input_folder, input_folder_err := os.open(input)
			log.ensuref(input_folder_err == nil, "Failed opening folder %v: %v", input, input_folder_err)
			iter := os.read_directory_iterator_create(input_folder)

			for f in os.read_directory_iterator(&iter) {
				if f.type != .Regular {
					continue
				}

				append(&input_files, filepath_join({input, f.name}))
			}

			os.close(input_folder)
		} else if os.is_file(input) {
			append(&input_files, input)
		} else {
			log.errorf("%v is neither directory or .h file", input)
		}
	}

	if output_folder != "" && !os.exists(output_folder) {
		make_dir_err := os.make_directory_all(output_folder)
		log.ensuref(make_dir_err == nil, "Failed creating output directory %v: %v", output_folder, make_dir_err)
	}

	for input_filename in input_files {
		if filepath.ext(input_filename) == ".h" {
			// We use a separate virtual arena for the types and the decls. When a dynamic array
			// reallocs using a virtual arena, then it can grow in-place if nothing else has been
			// allocated after it. Therefore these two have their own arena.
			//
			// Since they don't move in virtual memory when they grow, it means that pointers etc to
			// elements never get stale (given we don't remove things, which we don't, we just
			// append).
			types_arena: vmem.Arena
			types_arena_err := vmem.arena_init_static(&types_arena, 100 * mem.Megabyte)
			log.assertf(types_arena_err == nil, "Failed reserving types arena memory. Error: %v", types_arena_err)

			decls_arena: vmem.Arena
			decls_arena_err := vmem.arena_init_static(&decls_arena, 100 * mem.Megabyte)
			log.assertf(decls_arena_err == nil, "Failed reserving types arena memory. Error: %v", decls_arena_err)

			type_arr := make([dynamic]Type, allocator = vmem.arena_allocator(&types_arena))
			types := Type_List(&type_arr)
			decl_arr := make([dynamic]Decl, allocator = vmem.arena_allocator(&decls_arena))
			decls := Decl_List(&decl_arr)

			// Add dummy to both arrays so index `0` isn't a valid item. This means we can use index
			// `0` as a "no item found" marker.
			add_decl(decls, {})
			add_type(types, {})

			// Set allocator and temp allocator to an arena so we can just clear it after each lap
			// of this loop. This makes the memory management within each generation pass "script
			// like".
			gen_arena: vmem.Arena
			context.allocator = vmem.arena_allocator(&gen_arena)
			context.temp_allocator = vmem.arena_allocator(&gen_arena)
			gen_ctx = context
			
			log.infof("Collecting data from %v", input_filename)
			collect_res, collect_ok := translate_collect(input_filename, config, types, decls)

			if !collect_ok {
				continue
			}

			translate_macros(collect_res.macros, decls, types, config)

			log.infof("Processing data from %v", input_filename)
			process_res := translate_process(collect_res, config, types, decls)
	
			input_folder := filepath.dir(input_filename)
			filename_stem := filepath.stem(input_filename)
			footer_filename := filepath_join({input_folder, fmt.tprintf("%v_footer.odin", filename_stem)})

			footer: string
			if os.exists(footer_filename) {
				if footer_bytes, footer_bytes_err := os.read_entire_file(footer_filename, context.allocator); footer_bytes_err == nil {
					footer = string(footer_bytes)
				}
			}

			output_filename := filepath_join({output_folder, fmt.tprintf("%v.odin", filename_stem)})
			log.infof("Writing %v", output_filename)
			output(types, decls, process_res, output_filename, footer, package_name)
			vmem.arena_destroy(&gen_arena)
			vmem.arena_destroy(&types_arena)
			vmem.arena_destroy(&decls_arena)
		}
	}
}

gen_ctx: runtime.Context

to_cstring :: proc(str: string) -> cstring {
	return strings.clone_to_cstring(str)
}